plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id 'com.github.ben-manes.versions' version '0.52.0'
    id 'net.researchgate.release' version '3.1.0'
}

ext {
    jdk = JavaVersion.current().majorVersion
    jdk_javadoc = "https://docs.oracle.com/javase/$jdk/docs/api/".toString()
    if (JavaVersion.current().isJava11Compatible()) {
        jdk_javadoc = "https://docs.oracle.com/en/java/javase/$jdk/docs/api/".toString()
    }

    jackson_databind_version = '2.12.7.1'
    jackson_datatype_jsr310_version = '2.14.2'
    jackson_datatype_jdk8_version = '2.12.7'
    immutables_group = 'org.immutables'
    immutables_version = '2.10.1'

    junit_version = '5.7.1'
    logback_version = '1.2.3'

    isJitpack = "true" == System.getenv("JITPACK")
    isRelease = !version.toString().endsWith('-SNAPSHOT')
}

allprojects {
    apply plugin: 'java-library'
    apply plugin: 'com.github.ben-manes.versions'
    apply plugin: 'maven-publish'
    if (!isJitpack && isRelease) {
        apply plugin: 'signing'
    }

    group 'com.discord4j'
    description 'Discord entity domain as immutable Jackson objects'

    sourceCompatibility = 8

    repositories {
        mavenCentral()
        if (!isRelease || isJitpack) {
            maven { url 'https://central.sonatype.com/repository/maven-snapshots' }
            maven { url 'https://ossrh-staging-api.central.sonatype.com/service/local' }
        }
        maven { url 'https://jitpack.io' }
        maven { url 'https://repo.spring.io/milestone' }
    }

    dependencies {
        compileOnly "com.google.code.findbugs:jsr305:3.0.1"
        testCompileOnly "com.google.code.findbugs:jsr305:3.0.1"
    }

    configurations.all {
        resolutionStrategy.cacheChangingModulesFor 60, 'seconds'
    }

    tasks.withType(Javadoc) {
        options {
            encoding = 'UTF-8'
            tags = ["apiNote:a:API Note:",
                    "implSpec:a:Implementation Requirements:",
                    "implNote:a:Implementation Note:"]
            addStringOption 'Xdoclint:none', '-quiet'
            addStringOption 'encoding', 'UTF-8'
            // adding links to javadocs for filling specific classes when not existing inside current javadoc
            links = [
                    jdk_javadoc,
                    "https://javadoc.io/doc/com.fasterxml.jackson.core/jackson-databind/2.12.7/"
            ]
        }
        if (JavaVersion.current().isJava9Compatible()) {
            options.addBooleanOption('html5', true)
        }
    }

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
        options.incremental = true
    }

    java {
        withJavadocJar()
        withSourcesJar()
    }

    tasks.withType(Test) {
        useJUnitPlatform()
    }

}

dependencies {
    api project(':api')
    compileOnly project(':encoding')

    api "com.fasterxml.jackson.core:jackson-databind:$jackson_databind_version"
    implementation "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:$jackson_datatype_jsr310_version"
    implementation "com.fasterxml.jackson.datatype:jackson-datatype-jdk8:$jackson_datatype_jdk8_version"

    compileOnly "$immutables_group:value:$immutables_version"
    compileOnly "$immutables_group:builder:$immutables_version"
    annotationProcessor "$immutables_group:value:$immutables_version"

    testCompileOnly project(':encoding')
    testCompileOnly "$immutables_group:value:$immutables_version"
    testAnnotationProcessor "$immutables_group:value:$immutables_version"
    testImplementation "org.junit.jupiter:junit-jupiter-engine:$junit_version"
    testImplementation "ch.qos.logback:logback-classic:$logback_version"
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifactId archivesBaseName

            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }

            pom {
                name = "$project.name"
                description = 'Discord entity domain as immutable Jackson objects'
                url = 'https://github.com/Discord4J/discord-json'
                organization {
                    name = 'Discord4J'
                    url = 'https://discord4j.com'
                }
                issueManagement {
                    system = 'GitHub'
                    url = 'https://github.com/Discord4J/discord-json/issues'
                }
                licenses {
                    license {
                        name = 'LGPL-3.0'
                        url = 'https://github.com/Discord4J/discord-json/LICENSE.txt'
                        distribution = 'repo'
                    }
                }
                scm {
                    url = 'https://github.com/Discord4J/discord-json'
                    connection = 'scm:git:git://github.com/Discord4J/discord-json.git'
                    developerConnection = 'scm:git:ssh://git@github.com:Discord4J/discord-json.git'
                }
                developers {
                    developer {
                        name = 'The Discord4J Team'
                    }
                }
            }
        }
    }

    if (!isJitpack) {
        repositories {
            maven {
                if (isRelease) {
                    url 'https://ossrh-staging-api.central.sonatype.com/service/local'
                } else {
                    url 'https://central.sonatype.com/repository/maven-snapshots'
                }
                def sonatypeUsername = findProperty('sonatypeUsername')
                def sonatypePassword = findProperty('sonatypePassword')
                if (sonatypeUsername != null && sonatypePassword != null) {
                    credentials {
                        username sonatypeUsername
                        password sonatypePassword
                    }
                }
            }
        }
    }
}

jar {
    manifest {
        attributes 'Implementation-Title': 'discord-json',
                'Implementation-Version': archiveVersion,
                'Automatic-Module-Name': 'discord4j.discordjson'
    }
}

if (!isJitpack && isRelease) {
    signing {
        def signingKey = findProperty("signingKey")
        def signingPassword = findProperty("signingPassword")
        if (signingKey != null && signingPassword != null) {
            useInMemoryPgpKeys(new String(signingKey.decodeBase64()), signingPassword as String)
        }
        sign publishing.publications.mavenJava
    }
}

release {
    preTagCommitMessage = 'Release version'
    tagCommitMessage = 'Release version'
    newVersionCommitMessage = 'Next development version'
    git {
        requireBranch.set('')
    }
}

wrapper {
    distributionType = Wrapper.DistributionType.ALL
}
